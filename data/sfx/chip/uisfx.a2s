def title	"KRChipUISFX"
def version	"1.0.0"
def description	"Kobo Redux Chip Sound Theme - UI"
def author	"David Olofson"
def copyright	"Copyright 2016 David Olofson"
def license	"GPL"
def a2sversion	"1.9.1"

S_UI_OPEN()
{
	struct { wtosc }
	w triangle
	p -3
	a .6
	10 {
		+p .1
		d 10
	}
	a 0; d 100
}

S_UI_OK()
{
	struct { wtosc }
	w triangle
	p .5
	a .6
	15 {
		-p .3
		d 10
	}
	a 0; d 100
}

S_UI_MOVE()
{
	struct { wtosc }
	w noise
	a .05
	p 2
	d 100
	a 0; d 10
}

S_UI_TICK()
{
	struct { wtosc }
	w square
	p 3
	@a .05;	d 10
	@a 0
}

S_UI_CDTICK(P V=1)
{
	struct { wtosc }
	+tr P
	w pulse10
	a (V * .1); set a
	p 0; d 10
	p 1; d 10
	p 2; d 10
	p 3; d 10
	100 {
		*a .8; d 10
	}
	a 0; d 10
}

S_UI_GAMEOVER()
{
	struct { wtosc }
	w pulse10
	a .2
	p 0
	100 {
		-p .02
		d 10
	}
	a 0; d 10
}

S_UI_READY()
{
	struct { wtosc }
	tempo 120 32
	w saw
	a .2
	p -2
	15 {
			td 1
		+p 4n;	td 1
		+p 7n;	td 1
		-p 8n
	}
	10 {
		*a .5
		-p 6n
		3 {
				td 1
			+p 4n;	td 1
			+p 7n;	td 1
			-p 9n
		}
	}
	a 0
	d 20
}

S_UI_PLAY()
{
	struct { wtosc }
	tempo 120 16
	w saw
	a .1
	tr 0
	3 {
		p 0;	td 1
		p 4n;	td 1
		p 7n;	td 1
		p 4n;	td 1
		+tr 1
	}
	-tr 1
	5 {
		*a .25
		p 0;	td 1
		p 4n;	td 1
		p 7n;	td 1
		p 4n;	td 1
	}
	a 0
	d 20
}

S_UI_PAUSE()
{
	struct { wtosc }
	tempo 120 16
	w saw
	a .05
	tr 2
	2 {
		p 4n;	td 1
		p 7n;	td 1
		p 4n;	td 1
		p 0;	td 1
		+a .025
	}
	a .1
	3 {
		-tr 1
		p 4n;	td 1
		p 7n;	td 1
		p 4n;	td 1
		p 0;	td 1
	}
	-tr 1
	a 0
	d 20
}

S_UI_CANCEL()
{
	struct { wtosc }
	w triangle
	5 {
		p 1; a .15;	d 10
		p 0; a 0;	d 10
	}
}

S_UI_ERROR()
{
	struct { wtosc }
	w square
	10 {
		p 2.5; @a .15;	d 10
		p 1.5; a 0;	d 10
	}
}

S_UI_LOADER()
{
	struct { wtosc }
	w square; a .005
	for {
		if (rand 1 > .5) {
			p 2400f
		} else {
			p 1200f
		}
		d (1000 / 600)
	}
.stop
	a 0; d 1

	1() {
		force stop
	}
}

S_UI_NOISE()
{
	struct { wtosc }
	w noise; p 4
	!running 1
	while running {
		@a .1; a 0;	d 10
				d 10
	}

	1() {
		running 0
	}
}
